set(CM_PROJECT_NAME PELF)

# top-level check
if (CMAKE_SOURCE_DIR STREQUAL CMAKE_CURRENT_SOURCE_DIR)
    cmake_minimum_required(VERSION 3.25)
    project(${CM_PROJECT_NAME})
elseif (NOT PROJECT_NAME)
    project(${CM_PROJECT_NAME})
endif ()

set(CMAKE_CXX_STANDARD 20)

if (EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/CM_Config")
    file(GLOB CM_CONFIG_FILES "${CMAKE_CURRENT_SOURCE_DIR}/CM_Config/*.cmake")
    foreach (CM_CONFIG_FILE ${CM_CONFIG_FILES})
        include("${CM_CONFIG_FILE}")
    endforeach ()
endif ()

if (EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/Include")
    include_directories("${CMAKE_CURRENT_SOURCE_DIR}/Include")
endif ()

if (EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/External_Sources")
    file(GLOB EXTERNAL_SOURCE_MODULES "${CMAKE_CURRENT_SOURCE_DIR}/External_Sources/*")
    foreach (EXTERNAL_SOURCE_MODULE ${EXTERNAL_SOURCE_MODULES})
        if (EXISTS "${EXTERNAL_SOURCE_MODULE}/SourceInfo.cmake")
            set(PROJECT_MODULE_ROOT "${EXTERNAL_SOURCE_MODULE}")
            include("${EXTERNAL_SOURCE_MODULE}/SourceInfo.cmake")
            message(STATUS "External module indexed: ${PROJECT_MODULE_ROOT}")
        elseif (EXISTS "${EXTERNAL_SOURCE_MODULE}/CMakeLists.txt")
            add_subdirectory("${EXTERNAL_SOURCE_MODULE}")
            message(STATUS "External module indexed: ${EXTERNAL_SOURCE_MODULE}")
        endif ()
    endforeach ()
endif ()

if (EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/Sources")
    file(GLOB Private_CM_CONFIG_Modules "${CMAKE_CURRENT_SOURCE_DIR}/Sources/*")
    foreach (Private_CM_CONFIG_Module ${Private_CM_CONFIG_Modules})
        if (EXISTS "${Private_CM_CONFIG_Module}/SourceInfo.cmake")
            set(PROJECT_MODULE_ROOT "${Private_CM_CONFIG_Module}")
            include("${PROJECT_MODULE_ROOT}/SourceInfo.cmake")

            message(STATUS "Project Index successful: ${PROJECT_MODULE_ROOT}")
        endif ()
    endforeach ()
else ()
    message(FATAL_ERROR "The directory for sources not found. Make sure that the directory exists, and that at least one module is present.")
endif ()

if (EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/Targets")
    file(GLOB TARGET_MODULES "${CMAKE_CURRENT_SOURCE_DIR}/Targets/*.cmake")
    foreach (TARGET_MODULE ${TARGET_MODULES})
        if (EXISTS "${TARGET_MODULE}")
            include("${TARGET_MODULE}")
        endif ()
    endforeach ()
endif ()